---
layout: post
title: Solidity Programming
categories: Programming
---

Solidity is an object-oriented, high-level language for implementing smart contracts. Smart contracts are programs which govern the behaviour of accounts within the Ethereum state.
<!--continue-->


- [Introduction](#section-1)
- [Arrays](#section-2)
- [Reference Types](#section-3)
- [Constructors](#section-4)
- [Input Validations](#section-5)
- [Voting on Ethereum](#section-6)
- [Mappings](#section-7)
- [Using Mappings](#section-8)
- [Control Structures](#section-9)
- [Structs](#section-10)
- [Memory and Storage](#section-11)
- [Using Structs in Solidity](#section-12)
- [Payments with smart contracts](#section-13)
- [Multi-sig Wallet](#section-14)
- [Summary](#section-15)
- [Useful References](#section-16)


## 1. Introduction {#section-1}

 The following concepts of the language is necessary to dive deeper into solidity:
- Data structures
- Control Structures
- Structs
- Constructors
- Useful methods and features
- Payments with Ethereum

We can learn this while developing these two applications on solidity:
1. Voting application - to understand the data structures
2. Multi-sig wallet application - to understand about payments with ethereum

## 2. Arrays {#section-2}

### Arrays

Arrays are basically of 2 types :

1. Dynamic Array

```
uint[] dynamic;  //empty array
uint[] dynamic2 = new uint[](2);  //Array of size 2

// Get the first element of the array
uint i = array[0];

//set an element
array[3] = 6;

// Get lenght of an array
uint size = array.length;

// add element at the end of the array
uint newSize = array.push(32);
```


2.  Statically-sized array

```
uint[6] array6;  // array of length 6

//can't change length
arr6.length = 10;   // INVALID

// can't add new elements
arr6.push(32);     // INVALID

// Allowed operations
uint i = array[5];  // get an element
array[6] = 9;       // set an element
uint size = array.length;   // get size of array

```
Other array types can be:

**Multi-dimensional arrays**
```
uint[][] nested;
```
**Fixed sized bytes arrays**
```
bytes1 oneByte;
bytes2 twoBytes;
bytes32 manyBytes;
```
**Dynamic bytes array**
```
bytes dynamicBytes;
```
#### Solidity Limitations

1. Cannot pass dynamic array of arrays into a public smart contract function. (so cannot pass array of strings to public method)
2. Cannot return dynamic array of arrays. (cannot return array of strings)
3. Ethereum protocol however supports the above feature. However in the experimental versions of solidty, it is supported and can be enabled in following ways:

```
pragma experimental ABIEncoderV2;

contract Foo {
    function method() public view returns(string[]){
        ...
    }
}
```

## 3. Reference Types {#section-3}

The types that we use: uint, int, bool, are all value types while there are, array, mappings and structs which are refrenece  types.
The difference between a value type and reference type is the foloowing:

- If we have an instance of a value type and assign it to a varaible, this will create a copy of this instance. So if we change the value of this copy, it doesn't change the value of original.

```
uint a = 10;
uint b = a;
b += 1;
```



- If we have an array for example, and we assign it to another array, this will not copy it automatically. Instead the orinal and the assigned one will refer to the same original array. So this means if we change the later, the referenced one is changed as well.

```
uint[] a = new unit[](2);
uint[] b = a;
b[0] = 1;
```

## 4. Constructors {#section-4}

A constructor is a method which allows us to initialize fields of a contract when it's deployed.
- It is similar to object-oriented languages.
- In solidity, we can have only one constructor.

```
contract Foo {
    string value;

    // can only have one constructor
    constructor(string _value){

        //Initialize a contract's field
        value = _value;
    }
}
```

## 5. Input Validation {#section-5}

Before implemmenting a smart contract, we need to learn a way to reject a bad transactions. Users can try to execute invalid transactions in following ways:
- Pass invalid parameters
- Execute invalid state transition

When we need to reject these transactions, solidity provides us with following functions:
- revert()
- require()
- assert()

When we call any of these functions, they stop a transaction and revert any changes.

#### Assert/Require

Require is usually used to verify the arguments that are passed to your method.

- For require, we need to provide a boolean expression.
- If the expression is false, the transaction execution is stopped and all changes are reverted.
- When we use require, all unused gas at the point of when you call require will be refunded to user.

```
// verify arguments
require(optionPos < optionsCount);

require(optionPos < optionsCount, "Invalid option");
```

Assert is usually used to verify an internal state of a contract.
- And if assert is called, it will also stop transaction execution but it will not refund remaining gas.
- The reason behind this is, if you use assert to validate some core assumptions about the state of your contract, and if this is violated, then maybe somebody is trying to hack your contract and you want to punish the attacker.

```
//Verify internal state
assert(totalVotesNum == sumOfVotes);
// Use up all remaining gases
```

#### Revert/Throw

If you call revert, it unconditionally reverts the current transaction.
- usually it is used in if-block
- so, you need to provide a boolean expression, and if that expression is true, then only  the block is executed.
- if the expression is false, then revert works similar to require; stops transaction execution, reverts all the changes and refunds the remaining gas.

```
// unconditionally revert a transaction
revert();
if(optionPos >= optionsCount){
    revert();
}

// Deprecated keyword(will be removed)
throw;
```


## 6. Voting on Ethereum {#section-6}

We will use the things learned above to implement a simple voting application.
 - Implement a constructor.
 - Use arrays to store votes
 - use "require" to validate incoming transactions

 ```
// SPDX-License-Identifier: GPL-3.0
pragma solidity ^0.4.0;

pragma experimental ABIEncoderV2;

contract Voter {
    uint[] public votes;
    string[] public options;

    constructor(string[] _options) public {
        options = _options;
        votes.length = options.length;
    }

    function vote(uint option) public {
        require(0 <= option && option < options.length, "Invalid option");
        votes[option] = votes[option]+1;
    }

    function getOptions() public view returns(string[]){
        return options;
    }

    function getVotes() public view returns(uint[]){
        return votes;
    }
}
 ```

### Note 
In the above version of solidity, we cannot use dynamic array of arrays; **(string[] _options** in constructior above,
so we have defined **pramga experimenatal ABIEncoderV2** to enable this. Choose appropriate compiler version to compile this **Voter.sol** file and Javascript VM environent before deploying.


## 7. Mappings {#section-7}

The voting smart contract implemmented has one major issue that is, a single user account can vote multiple times. To restirct one user to vote only once, we need to store who has already voted. In diffrenet programming languages, we would be using hash map data structure (hash map, hash table) for this purpose.

In solidity, a hash map is called "Mapping". In this case we will use, __a map of address to Boolean to keep track of what address has already voted__.


#### "msg" Varaible

Solidity has a global object called **msg** that contains information that was sent to our smart contract.

```
function method(){
    // address of a sender of a transaction, in our case voter's address
    msg.sender();

    //number of wei send
    msg.value;

    // complete transaction data
    msg.data;

    // signature, contains numerical identifier of the function that was called by the caller of this transaction
    msg.sig;

    // contins remaining gas (Deprecated)
    msg.gas;
}
```

Now, we can keep track of who has already voted. To keep track of who has already voted, we will use **Mapping**

```
// Mapping
mapping ([key-type] => [value-type]) mapName;

// similar to HashMap, unordered_map, dict, etc. in other prgramming languages

//In our case, it would be
mapping(address => bool) has Voted
```

#### Operations with Mapping

```
mapping (string => uint) scoreFor;

// Store Value
scoreFor["Adam"] = 1;

// Get Value
uint userScore = scoreFor["Adam"];


// for non-existing keys, the default value defends on what data type is used for
// a value in a map
mapping (string => uint) scoreFor;
unit userScore = scoreFor["non-existing-user"];    // 0


// Default value in Solidity exist for all types
contract Foo {
    bool flag;    // false
    uint num;     // 0
    uint[] array;   // empty array
    string str;      // ""
}
```
#### What Mapping Does not support in Solidity

- Get total number of items
- Check if a key exists
- Get all keys

## 8. Using Mappings {#section-8}

We will modify the above contract to do the following:
- Track who voted using our contract
- use mapping to track voters (use mapping data structures

The following modified contract allows one account to vote only one time.

```
// SPDX-License-Identifier: GPL-3.0
pragma solidity ^0.4.0;

pragma experimental ABIEncoderV2;

contract Voter {
    uint[] public votes;
    string[] public options;

    mapping (address => bool) hasVoted;

    constructor(string[] _options) public {
        options = _options;
        votes.length = options.length;
    }

    function vote(uint option) public {
        require(0 <= option && option < options.length, "Invalid option");

        require(!hasVoted[msg.sender], "Account has already voted!");

        votes[option] = votes[option]+1;

        hasVoted[msg.sender] = true;
    }

    function getOptions() public view returns(string[]){
        return options;
    }

    function getVotes() public view returns(uint[]){
        return votes;
    }
}

```



## 9. Control Structures {#section-9}

Solidity supports 2 basic control structures. 
- If/Else

```
    if(totalVotes > limit){
        ...
    }
    else if(totalVotes == limit){
        ...
    }
    else{
        ...
    }
```
**We can only use boolean statement in If statement. Using numbers, strings or arrays in If conditiion is Illegal in solidity.**


```
// String Comparison

string str1 = "abc";
string str2 = "bcd";

if(str1 == str2) { ... }   // NOT Supported

bytes[32] hash = keccak256(str1);  // Hash Value
if(keccak256(str1) == keccak256(str2)) {...}

```


- Different types of loops

```
while (i<5>){
    res += i;
    i++;
}

do {
    res += i;
    i++;
}while(i>5);
```


```
// For Loops
uint[] arr = ...;
uint sum = 0;
for (uint i = 0; i < arr.length; i++){
    sum += arr[i];
}
```

## 10. Structs {#section-10}

In the voter contract, what if we want to vote by providing option name instead of number.
For this we will need  a new method that use the "struct" feature.

New "vote()" Method
```
uint[] public votes;
string[] public options;

// Need to initialize the mapping using a loop
mapping (string => uint) posOfOption;

function vote(string optionName) public {
    uint optionPos = posOfOption[optionName];

    //Implement the rest as before
}
```

Check if an Option Exists

```
mapping(string => uint) posOfOption;
posOfOption["first-option"];  // Returns 0
posOfOption["non-existent"];   // Returns 0 (default value)
```

**Structs in Solidity**
- A struct is a user-defined type in solidity that is just a collection of fields.
- A struct is like a class without methods.
- A struct is not just a flat collection of fields, it can also be nested i.e. filed in struct can be a struct.

**Using Struct in Solidity**

```
struct User {
    string name;
    string surname;
    uint8 age;
}

// create a user variable of type User Struct and create an instance of User
User user = User("Gagan","Magar","15");
User user = User({name: "Gagan", surname:"Magar", age:15 });

// get value of a field
uint age = user.age;

// set value of a field
user.age = 19;

```

***Default Values annd Structs***

```
struct Option {
    uint pos;
    bool exists;
}

mapping(string => Option) posOfOption;

posOfOption["no-existing"];    // Option(0, false);
posOfOption["option"] = Option(0,true);
posOfOption["option"];  // Option(0, true);
```


## 11. Memory and Storage {#section-11}

There are two types of storage in smart contract.

- Storage
    1. It is a contract state.
    2. The values written to storage are persistent between calls.
    3. Storage is expensive to use. Using command, SSTORE will cost upto 20,000 gas.

- Memory
    1. Memory is temporary variable.
    2. The valuse written to memory exists only during a method execution and when the method execution finishes, this value will disappear.
    3. Memory is cheap to use. Using command, MSTORE will only cost 3 gas.

```
contract Foo {

    // the field array here is stored in storage
    // if we write something to this array, the changes to this array
    // are persistent between method calls.
    uint[] array;

    function method(){

        // with an array stored in storage
        // we can use "memory" keyword before name of array to create
        // a temporary array in memory

        uint[] memory memoryArr = array;
        memoryArr[0] = 0;   // Modifies a copy

        // however if we use "storage" keyword, both array and storage array
        // will refer to the same array of numbers and changing the storage array
        // will change the original array stored in the contract field

        uint[] storage storageArr = array;
        storageArr[0] = 0;   // Modifies an original
    }
}
```

## 11. Using Structs in Solidity {#section-12}

Let's update the above voiting contract:
- implement the new method vote(string), which will allows user to vote not by providing an option position but by providing an option position.
- Use structs in Solidity

**Updating the above voter contract to use struct, mappings and new function "vote"**
```
// SPDX-License-Identifier: GPL-3.0
pragma solidity ^0.4.0;

pragma experimental ABIEncoderV2;

contract Voter {

    struct OptionPos {
        uint pos;
        bool exists;
    }


    uint[] public votes;
    string[] public options;

    mapping (address => bool) hasVoted;
    mapping (string => OptionPos) posOfOption;

    constructor(string[] _options) public {
        options = _options;
        votes.length = options.length;

        for (uint i=0; i<options.length; i++){
            OptionPos memory optionPos = OptionPos(i, true);
            string optionName = options[i];
            posOfOption[optionName] = optionPos;
        }
    }

    function vote(uint option) public {
        require(0 <= option && option < options.length, "Invalid option");

        require(!hasVoted[msg.sender], "Account has already voted!");

        votes[option] = votes[option]+1;

        hasVoted[msg.sender] = true;
    }

    function vote(string optionName) public {
        require(!hasVoted[msg.sender],"Account has already voted");

        OptionPos memory optionPos = posOfOption[optionName];
        require(optionPos.exists, "Option does not exist");

        votes[optionPos.pos] = votes[optionPos.pos]+1;
        hasVoted[msg.sender] = true;
    }

    function getOptions() public view returns(string[]){
        return options;
    }

    function getVotes() public view returns(uint[]){
        return votes;
    }
}

```

## 12. Payments with smart contracts {#section-13}

- How to send and recieve funds in with Ethereum
- Implemment a simple multi-sig wallet(contract where we can put our funds but to get funds from this contract, several approvals are required)

![Payment with Smart Contracts](/assets/images/smart-contract-payment.png "payment with smart contract")

**Accepting a Payment**

```
function buyGadgets(unit gadgetsNum) payable {
    // check send amount
    require(msg.value > 100);

    // Payment is accepted
}
```
**Fallback Function**
Called when ether is sent without calling a method

```
function() payable {
    require(msg.value < 1000);
}
```

**Address Type**

```
address addr = msg.sender;

// Get account balance
uint accountBalance = addr.balance;

// Contract's balance
address(this).balance
```

**Sending Payments**

```
address addr = ...;

// Reverts execution if fails
addr.transfer(1000);   // weis not ether

// Returns "false" if fails
bool succeeded = addr.send(1000);

// should check return value!
```
__send() method is unsafe and should be avoided. Use transfer() when possible.__

## 13. Multi-sig Wallet {#section-14}

Lets implement a multi-signature wallet; a contract that can send and receive payments.

1. First a user will create a smart contract and send some amount of ehter to this contract.
2. The creator of the contract should also provide beneficiary and approvers.
3. The approvers from the list can interact with the contract to approve or reject the transaction.
4. If the necessary amount of approvers approve the transaction, then the fund will be sent to the beneficiary.
5. If any of the approver rejects the transaction, then the fund will go back to the creator of the smart contract.

```
pragma solidity ^0.4.0;
pragma experimental ABIEncoderV2;

contract MultiSigWallet {

    uint minApprovers;

    address beneficiary;
    address owner;

    // mapping of address to boolean to find out who already approved the transaction
    // if this mapping contains true, this means this address has already approved a transaction
    mapping(address => bool) approvedBy;

    // mapping which we will be build in our constructor which contains address of all approvers
    // so that we can check if the particular address is approver or not
    mapping(address => bool) isApprover;

    // to get number of approvals so far, 0 by default
    uint approvalsNum;

    // our constructor have list of approvers, minimum number of approvers and
    // the address of beneficiary

    constructor(
        address[] _approvers,
        uint _minApprovers,
        address _beneficiary
    ) public payable {

        require(_minApprovers <= _approvers.length,
            "Required number of approvers should be less than number of approvers."
        );

        //initialize the minimum no. of approvals and the beneficiary address
        minApprovers = _minApprovers;
        beneficiary = _beneficiary;

        // store the address of an account who created this contract
        // sender is used to find out who created the transaction but 
        // in the constructor, sender can be used to find out who created the contract
        owner = msg.sender;

        // populate isApprover mapping to store all the address of the approvers
        for(uint i=0; i<_approvers.length; i++){
            address approver = _approvers[i];
            isApprover[approver] = true;
        }

    }


    // method to approve a transaction by approvers
    function approve() public {

        //check if the caller has right to approve or reject transactions
        // use require and isApprover map, 
        // if the message sender is in the isApprover map, return true
        // if message sender is not in the isApprover map, default false is set and 
        // transaction is reverted
         require(isApprover[msg.sender],"Not an approver");


         // increment the number of approvals but only if this approver hasn't approved
         // this transaction before.
         if(!approvedBy[msg.sender]){
             approvalsNum++;
             
             // to ensure that the same approver doesn't approve the same transaction twice,
             // store an address of an approver in the approvedBy mapping 
             approvedBy[msg.sender] = true;
         }


        // check if got the necessary number of approvals
         if(approvalsNum ==minApprovers){

             // if we got minimum number of approvals, send all existent funds from this contract
             // to the beneficiary address and send the whole balance of this contract
             beneficiary.send(address(this).balance);

            // send method can fail due to invalid address etc. but it can be the case that our
            // smart contract still contains all the funds, In this case, we want to destory the smart
            // contract and return all the  existent funds to the owner of the contract.
             selfdestruct(owner);

             // normally we need to check the return value of the send method, but in this case we don't care
             // if fails, we will just return all the funds to the owner
             // if it succeeds we will send all the funds to the beneficiary and the balance of our smart contract
             // is 0 so the selfdestruct method will destroy the contract.
         }
    }

    // method to reject a transaction by approvers
    function reject() public {

        //check if the caller has right to approve or reject transactions
        // use require and isApprover map, 
        // if the message sender is in the isApprover map, return true
        // if message sender is not in the isApprover map, default false is set and 
        // transaction is reverted
        require(isApprover[msg.sender],"Not an approver");

        // if the caller of this method is an approver
        // we can use selfdestruct function that will destroy the smart contract, but
        // need to provide the address of an account to which we want to send the remaining funds.
        // we want to return all the existent funds to the creator of this contract,
        // if the transaction was not approved
        selfdestruct(owner);
    }
 
}
```



## 14. Summary {#section-15}

- We have learned many advanced solidity features like data structures, control structures, constructors, validation of method calls like assert()/require()/revert() and also storage types in solidity.
- We have been using Remix IDE so far, but for to level up our development, we need to use proper developement tools to buid, test and deploy contracts.
- We also need to learn web3 API which allows us to access Ethereum Network.
- Truffle; most popular build tool to work on contracts.


## 16. Useful References {#section-16}

- Official Solidity Documentation : [Solidity Documentation](https://docs.soliditylang.org/en/v0.8.11/)
- Blockchain Applications: [Stateofthedapps](https://www.stateofthedapps.com/)